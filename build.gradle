buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
  }
}

apply plugin: "com.gradle.plugin-publish"

apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    mavenCentral ()
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.maven:maven-jxr:2.5'

    testCompile 'junit:junit:4.12'
}

task packageSources (type: Jar) {
   from sourceSets.main.allSource
   classifier = 'sources'
}

artifacts {
   archives (packageSources)
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
}

// Set the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
  website = 'http://github.com/davidecavestro/gradle-jxr-plugin'
  vcsUrl = 'https://github.com/davidecavestro/gradle-jxr-plugin.git'
  description = 'A Gradle plugin that uses Maven JXR to produce cross-reference for project\'s sources.'
  tags = ['jxr', 'xref', 'cross-reference']

  plugins {
    jxrPlugin {
      id = 'net.davidecavestro.gradle.jxr'
      displayName = 'Gradle JXR plugin'
    }
  }
}


if(project.hasProperty("userRemoteRepo")) {
    
    
    def installer = install.repositories.mavenInstaller
    def deployer = null
    
    uploadArchives {
        repositories {
            deployer = mavenDeployer {
                name = 'bintrayDeployer'
                repository(url: bintrayReleaseUrl) {
                    try {
                        authentication(userName: bintrayUsername, password: bintrayPassword)
                    } catch (MissingPropertyException pne) {
                        // ignore, don't authenticate
                    }
                }
            }
        }
    }

    [installer, deployer].each { repo ->
        configure(repo) {
            pom.project {
                name('gradle-jxr-plugin')
                description("A Gradle plugin that uses Maven JXR to produce cross-reference for project's sources.")
                url('http://github.com/davidecavestro/gradle-jxr-plugin')
                inceptionYear '2013'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm { url "https://github.com/davidecavestro/gradle-jxr-plugin.git" }
                developers {
                    [
                        davidecavestro: 'Davide Cavestro', baron1405: 'Baron Roberts'
                    ].each { devId, devName ->
                        developer {
                            id devId
                            name devName
                            roles { role 'Developer' }
                        }
                    }
                }
            }
        }
    }
} else {
    uploadArchives {
        repositories { add project.repositories.fileRepo }
    }
}
